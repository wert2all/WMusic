apply plugin: 'com.android.application'

def computeVersionCode() {
    return (System.getenv("BUILD_NUMBER") as Integer ?: 1)
}

def computeVersionName(code) {
    return '1.0.' + (code as Integer)
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.wert2all.wmusic"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode computeVersionCode()
        versionName computeVersionName(computeVersionCode())
    }
    signingConfigs {
        release {
            try {
                storeFile file("other/wmusic.jks")
                storePassword KEYSTORE_PASSWORD
                keyAlias "appkey"
                keyPassword KEY_PASSWORD
            }
            catch (ignored) {
                throw new InvalidUserDataException("You should define KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties.")
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
        }
        release {
            minifyEnabled false
            zipAlignEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.2.0'
}
